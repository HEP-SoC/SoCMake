function(get_all_targets_of_group OUTVAR GROUP_NAME)
    get_all_targets(all_targets)

    unset(help_targets)
    foreach(target ${all_targets})
        get_property(target_groups TARGET ${target} PROPERTY SOCMAKE_GROUPS)

        if(${GROUP_NAME} IN_LIST target_groups)
            list(APPEND help_targets ${target})
        endif()
    endforeach()

    set(${OUTVAR} ${help_targets} PARENT_SCOPE)
endfunction()

function(group_custom_targets GROUP_NAME)
    cmake_parse_arguments(ARG "" "PATTERN" "TARGET_LIST" ${ARGN})
    if(ARG_UNPARSED_ARGUMENTS)
        message(FATAL_ERROR "${CMAKE_CURRENT_FUNCTION} passed unrecognized argument " "${ARG_UNPARSED_ARGUMENTS}")
    endif()

    if(ARG_PATTERN AND ARG_TARGET_LIST)
        message(FATAL_ERROR "Arguments PATTERN and TARGET_LIST cannot be used at the same time")
    endif()

    if(ARG_PATTERN)
        get_all_targets(all_targets)
        list(FILTER all_targets INCLUDE REGEX "${ARG_PATTERN}")
        set(targets ${all_targets})
    elseif(ARG_TARGET_LIST)
        set(targets ${ARG_TARGET_LIST})
    else()
        message(FATAL_ERROR "Specify either PATTERN or TARGET_LIST arguments")
    endif()

    if(NOT targets)
        message(WARNING "No targets found for PATTERN: ${ARG_PATTERN} or TARGET_LIST: ${ARG_TARGET_LIST}")
        return()
    endif()

    foreach(target ${targets})
        set_property(TARGET ${target} APPEND PROPERTY SOCMAKE_GROUPS ${GROUP_NAME})
    endforeach()
endfunction()

function(group_custom_ips GROUP_NAME)
    cmake_parse_arguments(ARG "" "PATTERN" "IP_LIST" ${ARGN})
    if(ARG_UNPARSED_ARGUMENTS)
        message(FATAL_ERROR "${CMAKE_CURRENT_FUNCTION} passed unrecognized argument " "${ARG_UNPARSED_ARGUMENTS}")
    endif()

    if(ARG_PATTERN AND ARG_IP_LIST)
        message(FATAL_ERROR "Arguments PATTERN and IP_LIST cannot be used at the same time")
    endif()

    if(ARG_PATTERN)
        get_all_ips(all_ips)
        list(FILTER all_ips INCLUDE REGEX "${ARG_PATTERN}")
        set(ips ${all_ips})
    elseif(ARG_IP_LIST)
        set(ips ${ARG_IP_LIST})
    else()
        message(FATAL_ERROR "Specify either PATTERN or IP_LIST arguments")
    endif()

    if(NOT ips)
        message(WARNING "No ips found for PATTERN: ${ARG_PATTERN} or IP_LIST: ${ARG_IP_LIST}")
        return()
    endif()

    foreach(ip ${ips})
        set_property(TARGET ${ip} APPEND PROPERTY SOCMAKE_GROUPS ${GROUP_NAME})
    endforeach()
endfunction()
